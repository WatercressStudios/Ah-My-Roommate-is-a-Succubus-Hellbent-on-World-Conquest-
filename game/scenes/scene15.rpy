label scene15:

scene dormhallway with dissolve
play ambient partybass fadein 1.0
"Before we could even get inside my dorm room, I could tell preeeetty quickly that something was off."
"For one, I heard some {i}real{/i} deep bass going down. Could feel it too."

show lucca h nervous with dissolve:
    align (0.2, 1.0)
voice "voice/c-15-1.ogg" #Lucca (Victoria Wong)
luc "Where's it coming from?"

show stacey neutrallook with dissolve
voice "voice/c-15-2.ogg" #Stacey (Ashe Thurman)
sta "Dunno, but I got a bad feeling about this."

show stacey neutral
voice "voice/c-15-3.ogg" #Stacey (Ashe Thurman)
sta "Like, artists are {i}waayyy{/i} bigger on popping on lo-fi hiphop beats and chilling. Or getting stoned. Or depressed."

show moe sighsweat with easeinright:
    align (0.75, 1.0)
voice "voice/c-15-4.ogg" #Moe (CJ Heineman)
moe "They have enough clubs downtown. Why here?"

voice "voice/c-15-5.ogg" #Yumi (Kathy Pfautsch)
yum "...Oh God, I think I know where it's coming from... {i}Kamika...{/i}..."

scene dormhallway with dissolve
"As the four of us staggered closer to my dorm room, the beats only grew with intensity."

voice "voice/c-15-6.ogg" #Yumi (Kathy Pfautsch)
yum "...Ohhh boy..."

voice "voice/c-15-7.ogg" #Moe (CJ Heineman)
moe "Well, don't just stand there gawking. Open the door."

voice "voice/c-15-8.ogg" #Stacey (Ashe Thurman)
sta "Don't you have, like, a master key or something?"

voice "voice/c-15-9.ogg" #Lucca (Victoria Wong)
luc "I-it would be pretty rude to walk in on a girl's dorm."

voice "voice/c-15-10.ogg" #Yumi (Kathy Pfautsch)
yum "Doesn't matter. It's unlocked."


play sound dooropen
"I open the door with a cautious squeeze of the handle..."

#cg rave? special bg?
play music rave fadein 2.0
scene dormroomparty with dissolve

voice "voice/c-15-11.ogg" #Yumi (Kathy Pfautsch)
yum "...Eh?"

stop ambient
"The room was packed with people. So many people. Too many."
"And there was such an ungodly racket! Was that music??"

show moe sigh with dissolve:
    align (0.60, 1.0)
voice "voice/c-15-12.ogg" #Moe (CJ Heineman)
moe "Ugh, even Kamika's playlist is tacky."

show stacey eyebrow with dissolve:
    align (0.35, 1.0)
voice "voice/c-15-13.ogg" #Stacey (Ashe Thurman)
sta "Hey Moe, there's, uh, a lot of people in here. Isn't that, like, some kind of safety violation?"

show moe neutral
voice "voice/c-15-14.ogg" #Moe (CJ Heineman)
moe "Bold of you to assume I read the entirety of the student handbook."

voice "voice/c-15-15.ogg" #Yumi (Kathy Pfautsch)
yum "Does it matter!? There are {i}random people{/i} in my {i}dorm room!{/i} Who are these people!?"

voice "voice/c-15-16.ogg" #Yumi (Kathy Pfautsch)
yum "{b}KAMIKA!{/b} WHERE ARE YOU!?"

voice "voice/c-15-17.ogg" #Kamika (Ariane Marchese)
kam "Ooh!~ The guest of honor!"

show moe neutrallook:
    xzoom -1.0
show kamika u happyclosed with easeinright:
    align (1.1, 1.0)
    xzoom 1.0
"With a flourish, Kamika parts the crowd like the goddamn Red Sea and saunters over, stopping in front of us with a coquettish wink."

voice "voice/c-15-18.ogg" #Kamika (Ariane Marchese)
kam "Come to enjoy the party? It's positively {i}smashing!{/i}"

voice "voice/c-15-19.ogg" #Yumi (Kathy Pfautsch)
yum "Cut the crap! Get them out, I got works in progress up in here!"

show kamika u sigh:
    xzoom -1.0
voice "voice/c-15-20.ogg" #Kamika (Ariane Marchese)
kam "Oh relaaax, I moved everything aside. Honestly, you can be such a {i}downerrr{/i}."

show kamika u evilsmile:
    xzoom 1.0
voice "voice/c-15-21.ogg" #Kamika (Ariane Marchese)
kam "C'mon, try the fruit punch! You'll feel better!~"

show stacey confused
show moe squint:
    xzoom -1.0
voice "voice/c-15-22.ogg" #Yumi (Kathy Pfautsch)
yum "What? No! Why!? Y-you probably put something in it!"

show kamika u badliar:
    xzoom 1.0
voice "voice/c-15-23.ogg" #Kamika (Ariane Marchese)
kam "N, no I didn't!"

show moe smuglook:
    xzoom -1.0
voice "voice/c-15-24.ogg" #Moe (CJ Heineman)
moe "Actually, I'm feeling a little parched. Punch me."

show kamika u wideeyes:
    xzoom 1.0
show stacey eyebrow
show moe smugclosed with easeinright:
    align (0.65, 1.0)
    xzoom -1.0
"Moe reached out and took the cup, to my utter astonishment. Kamika looked pretty surprised as well."

show kamika u wideeyes with easeinright:
    align (1.2, 1.0)
    xzoom 1.0
voice "voice/c-15-25.ogg" #Kamika (Ariane Marchese)
kam "Eh? Really?"

voice "voice/c-15-26.ogg" #Yumi (Kathy Pfautsch)
yum "MOE! What are you {i}doing!?{/i} You don't know what she's mixed in with that stuff!"

show moe neutral:
    xzoom -1.0
voice "voice/c-15-27.ogg" #Moe (CJ Heineman)
moe "Mm."

show stacey neutral
"He just shrugs at me?? What is his deal!?"

show stacey smug
voice "voice/c-15-28.ogg" #Stacey (Ashe Thurman)
sta "I mean, even if she spiked the punch with like, rum or whatever, we're all adults, right?"

show kamika u evilgrin:
    xzoom 1.0
voice "voice/c-15-29.ogg" #Kamika (Ariane Marchese)
kam "Mhm, mhm, mhm~... yes. Rum. Precisely. And a {i}very special ingredient!~{/i}"

voice "voice/c-15-30.ogg" #Yumi (Kathy Pfautsch)
yum "...Oh my God that's actually disgusting. And unsanitary."

show stacey therocklook
voice "voice/c-15-31.ogg" #Stacey (Ashe Thurman)
sta "I mean, it sounds like she doesn't skimp out on the good stuff..."

voice "voice/c-15-32.ogg" #Yumi (Kathy Pfautsch)
yum "{i}Guys.{/i} What the Hell. She put her {i}sweat{/i} in it."

show kamika u glare:
    xzoom -1.0
voice "voice/c-15-33.ogg" #Kamika (Ariane Marchese)
kam "Ugh, don't call it that. They're {i}pheromones{/i} you idiot."

show stacey mad
voice "voice/c-15-34.ogg" #Stacey (Ashe Thurman)
sta "Oh, man. Yuck."

show lucca h nervouser with easeinleft:
    align (0.1, 1.0)
voice "voice/c-15-35.ogg" #Lucca (Victoria Wong)
luc "O-oh nooo, that isn't good... that's really really bad!"

show moe neutralclosed:
    xzoom 1.0
voice "voice/c-15-36.ogg" #Moe (CJ Heineman)
moe "...Eh."

show stacey eyebrowclosed
voice "voice/c-15-37.ogg" #Stacey (Ashe Thurman)
sta "D-dude, you're still drinking it?"

show moe happy
voice "voice/c-15-38.ogg" #Moe (CJ Heineman)
moe "It's free food and drink. Like, what else do you go to a party for?"

show stacey eyebrow
voice "voice/c-15-39.ogg" #Stacey (Ashe Thurman)
sta "...Touche?"


play sound stomping
scene dormroomparty with dissolve
"Unbelievable. I stomped up to Kamika and pressed a finger right into her chest."

voice "voice/c-15-40.ogg" #Yumi (Kathy Pfautsch)
yum "{i}You.{/i}"

show kamika u squirm with dissolve:
    xzoom -1.0
voice "voice/c-15-41.ogg" #Kamika (Ariane Marchese)
kam "Ooh!~ Touch me more, I'm so sensitive!"

voice "voice/c-15-42.ogg" #Yumi (Kathy Pfautsch)
yum "You're the one who brought all these people here! So send them all back!"

show kamika u smugclosed:
    xzoom 1.0
voice "voice/c-15-43.ogg" #Kamika (Ariane Marchese)
kam "What? Now?? When I teeter on the precipice of ultimate triumph? Puh-leeeease!"

show kamika u confident:
    xzoom -1.0
voice "voice/c-15-44.ogg" #Kamika (Ariane Marchese)
kam "Look at aaaaall these people, having a grand old time! And they're here for {i}me!~{/i}"

voice "voice/c-15-45.ogg" #Yumi (Kathy Pfautsch)
yum "They're here because you freakin' brainwashed them or something!"

show kamika u happyclosed:
    xzoom 1.0
voice "voice/c-15-46.ogg" #Kamika (Ariane Marchese)
kam "Oh don't be so dramatic. You could stand to learn something from them, and loosen up a little. Just go with things!~"

voice "voice/c-15-47.ogg" #Yumi (Kathy Pfautsch)
yum "This is {i}my{/i} dorm room! You're a, a goddamn freeloader!"

voice "voice/c-15-48.ogg" #Yumi (Kathy Pfautsch)
yum "Take them all out to a dumpster out back, have a grand old time there, see if I care!"

show kamika u pout:
    xzoom -1.0
voice "voice/c-15-49.ogg" #Kamika (Ariane Marchese)
kam "Mm... oh, poor misguided Yumi. We could've been the best of friends, the closest of partners..."

show kamika u seducesquint:
    xzoom 1.0
voice "voice/c-15-50.ogg" #Kamika (Ariane Marchese)
kam "If only you opened your eyes and saw the world for how it truly was.~"

show stacey neutral with easeinleft:
    align (0.2, 1.0)
voice "voice/c-15-51.ogg" #Stacey (Ashe Thurman)
sta "Oh boy, she's flexing her supervillain monologue..."

show moe neutralclosed with easeinleft:
    align (0.0, 1.0)
    xzoom -1.0
voice "voice/c-15-52.ogg" #Moe (CJ Heineman)
moe "Eh. Let her have her fun."

voice "voice/c-15-53.ogg" #Yumi (Kathy Pfautsch)
yum "W-what does that mean!?"

scene dormroomparty with dissolve
voice "voice/c-15-54.ogg" #Yumi (Kathy Pfautsch)
yum "Ugh, forget this. I'll kick everyone out myself."

show lucca h happy with easeinleft:
    align (0.3, 1.0)
voice "voice/c-15-55.ogg" #Lucca (Victoria Wong)
luc "A-and I'll help!"

show kamika u sigh with dissolve:
    align (0.7, 1.0)
    xzoom 1.0
voice "voice/c-15-56.ogg" #Kamika (Ariane Marchese)
kam "Oh. And Yumi's little toadie comes in to lend her valuable assistance. I expect so little from the both of you..."

show lucca h nervous
voice "voice/c-15-57.ogg" #Lucca (Victoria Wong)
luc "Ah... I-I'll show you!"

voice "voice/c-15-58.ogg" #Yumi (Kathy Pfautsch)
yum "Yeah, don't take that shit from her! C'mon Lucca!"

voice "voice/c-15-59.ogg" #Lucca (Victoria Wong)
luc "Right! I'll go with you!"

#hide lucca;
scene dormroomparty with dissolve
"Lucca and I charge into the crowds, leaving Stacey and Moe behind to... do something. Presumably heroic. Hopefully."

show stacey neutrallook with dissolve:
    align (0.3, 1.0)
voice "voice/c-15-60.ogg" #Stacey (Ashe Thurman)
sta "...Uh, we should probably go help them. Friends do that, right?"

show moe smugclosed with dissolve:
    align (0.7, 1.)
voice "voice/c-15-61.ogg" #Moe (CJ Heineman)
moe "Yeah, prolly. Let's finish off the snack table. And the punch."

show stacey eyebrowlook
voice "voice/c-15-62.ogg" #Stacey (Ashe Thurman)
sta "What, you serious? With like, Kamika's sweat in it??"

show moe mad
voice "voice/c-15-63.ogg" #Moe (CJ Heineman)
moe "You can barely taste it, don't be such a baby."

show stacey sarcastic
voice "voice/c-15-64.ogg" #Stacey (Ashe Thurman)
sta "...Maybe some weed will cleanse the palate later..."

scene dormroomparty with dissolve
"The two of us split up. We figured we could try a pincer movement to corral the crowds out the door, but..."

show lucca h happy with easeinright:
    align (0.8, 1.0)
    xzoom -1.0
voice "voice/c-15-65.ogg" #Lucca (Victoria Wong)
luc "O-okay everyone! I-if you could all get in a single-file line and funnel through the door, that... would be... cool?"

##voice "voice/c-15-66.ogg" #Partygoer 1 (???)
pg1 "SHOW US YOUR TITS!"

show lucca h madblush:
    xzoom 1.0
voice "voice/c-15-67.ogg" #Lucca (Victoria Wong)
luc "W-wha!? No! Screw you!"

##voice "voice/c-15-68.ogg" #Partygoer 2 (???)
pg2 "Oh, shit, you aren't Kamika, sorry. My bud's pretty wasted."

##voice "voice/c-15-69.ogg" #Partygoer 1 (???)
pg1 "TIIIIIIIIIIIITS!"

show lucca h uwah
show kamika u happyclosed with easeinright:
    xzoom -1.0
voice "voice/c-15-70.ogg" #Kamika (Ariane Marchese)
kam "Well, don't mind if I do!~"

"Was something going on at Lucca's position??"

scene dormroomparty with dissolve
##voice "voice/c-15-71.ogg" #Partygoer 1 (???)
pg1 "WOOOOOOO YEAAAAAAAAAAH!"

voice "voice/c-15-72.ogg" #Lucca (Victoria Wong)
luc "Oh God no- KAMIKA! Have some pride, damn it! This is an all-ages game!"

show laptop playlist with dissolve
"At least Lucca provided enough of a distraction for me to head over to the laptop."

voice "voice/c-15-73.ogg" #Yumi (Kathy Pfautsch)
yum "Haha, yes! Her playlist! All I have to do is..."

#cut music
stop music

show laptop default
"With a click of a button, I powered down the laptop, abruptly cutting off the steady flow of beats to the room."

"Everyone stopped what they were doing, and looked around, confused. They looked over to me."

voice "voice/c-15-74.ogg" #Yumi (Kathy Pfautsch)
yum "Alright! Party's over, everyone out!"

##voice "voice/c-15-75.ogg" #Partygoer 2 (???)
pg2 "Aw, you serious? It's not even ten yet..."

voice "voice/c-15-76.ogg" #Yumi (Kathy Pfautsch)
yum "DID I STUTTER!? {i}OUT!{/i}"

"A crowd of confused eyes stared at me. I surveyed them all, holding my ground."

hide laptop with dissolve
"...A few were eying the door! And some stragglers were on their way out!"

show kamika u thinkingclosed with dissolve
voice "voice/c-15-77.ogg" #Yumi (Kathy Pfautsch)
yum "Haha! Yes! How do you like that, Kamika!?"

voice "voice/c-15-78.ogg" #Yumi (Kathy Pfautsch)
yum "...Kamika?"

"Kamika didn't respond. She was... looking at her phone."

voice "voice/c-15-79.ogg" #Kamika (Ariane Marchese)
kam "...Just need to set this, aaaand..."

#music renews
play music rave fadein 1.0

show kamika u smug
voice "voice/c-15-80.ogg" #Kamika (Ariane Marchese)
kam "...Perfect. Party's back on, everyone!~"

hide kamika with easeoutright
voice "voice/c-15-81.ogg" #Yumi (Kathy Pfautsch)
yum "W-w-what?? How'd she do that!?"

voice "voice/c-15-82.ogg" #Yumi (Kathy Pfautsch)
yum "...Wait. Bluetooth speakers. God {i}damn{/i} it..."

show lucca h aaaa with easeinleft:
    align (0.3, 1.0)
voice "voice/c-15-83.ogg" #Lucca (Victoria Wong)
luc "W-we have to destroy the speakers, Yumi!"

voice "voice/c-15-84.ogg" #Yumi (Kathy Pfautsch)
yum "Fuck that, they're {i}my speakers!{/i} Are you insane!?"

show kamika u evilsmileclosed with easeinright:
    align (0.7, 1.0)
    xzoom 1.0
voice "voice/c-15-85.ogg" #Kamika (Ariane Marchese)
kam "Uehehehe!~ Watching you two squirm is sooooo fun to watch!"

if route == "stacey":
    jump s15stacey
elif route == "lucca":
    jump s15lucca
elif route == "kamika":
    jump s15kamika
else:
    jump s15kamika
#variation: if on stacey route

label s15stacey:
show kamika u smugclosed:
    xzoom -1.0
voice "voice/c-15-86.ogg" #Kamika (Ariane Marchese)
kam "And look! Look at all this I was able to accomplish, all by myself, no thanks to you!"

show kamika u smug:
    xzoom 1.0
voice "voice/c-15-87.ogg" #Kamika (Ariane Marchese)
kam "You could've been part of something grand! Something bigger than either of us!"

show kamika u happyclosed:
    xzoom -1.0
voice "voice/c-15-88.ogg" #Kamika (Ariane Marchese)
kam "...Well, bigger than you, at least. I'm plenty big.~"

voice "voice/c-15-89.ogg" #Yumi (Kathy Pfautsch)
yum "Actually, you're flat like a cutting board, but that's neither here nor there..."

show kamika u angrysurprise:
    xzoom 1.0
show lucca h neutral
voice "voice/c-15-90.ogg" #Kamika (Ariane Marchese)
kam "I-I'm not flat! Are you blind!?"

show kamika u shoutclosed:
    xzoom -1.0
voice "voice/c-15-91.ogg" #Kamika (Ariane Marchese)
kam "And besides! This party's a monument to my glory. It's a shame you didn't stick around, you could've made a great servant."

show kamika u sigh:
    xzoom 1.0
show lucca h sad
voice "voice/c-15-92.ogg" #Kamika (Ariane Marchese)
kam "Instead you idled away with a stoner doing... ah crap, I don't even know what she does."

voice "voice/c-15-93.ogg" #Yumi (Kathy Pfautsch)
yum "She's a writer you jackass!"

show kamika u smugsquint:
    xzoom -1.0
voice "voice/c-15-94.ogg" #Kamika (Ariane Marchese)
kam "Ohhh! They're a dime a dozen!~ I'll have enough bards writing songs about me, so she isn't that important to me."

voice "voice/c-15-95.ogg" #Yumi (Kathy Pfautsch)
yum "Oh my God, your bloated ego is insufferable..."
jump s15merge

#variation: if on lucca route (counter-demon squad)
label s15lucca:
show kamika u smugsquint:
    xzoom 1.0
voice "voice/c-15-96.ogg" #Kamika (Ariane Marchese)
kam "But I guess you wanna try to be the good guy? Of course you believe everything Lucy tells you."

voice "voice/c-15-97.ogg" #Yumi (Kathy Pfautsch)
yum "Her name's Lucca, get it right."

show kamika u sigh:
    xzoom -1.0
show lucca h neutral
voice "voice/c-15-98.ogg" #Kamika (Ariane Marchese)
kam "Oh, really? Poor thing must have an awful identity crisis. Always wants to prove herself to everyone."

show kamika u neutral:
    xzoom 1.0
voice "voice/c-15-99.ogg" #Kamika (Ariane Marchese)
kam "It's quite pitiful. You know this is just her own little ego trip, right? So she can feel self-important!"

voice "voice/c-15-100.ogg" #Yumi (Kathy Pfautsch)
yum "You're talking out of your ass! She doesn't want to see you embarrass yourself!"

show kamika u deadpan:
    xzoom -1.0
voice "voice/c-15-101.ogg" #Yumi (Kathy Pfautsch)
yum "But I guess it's a little late for that, huh?"

show kamika u glare:
    xzoom 1.0
voice "voice/c-15-102.ogg" #Kamika (Ariane Marchese)
kam "Grr! How dare you??"
jump s15merge

#variation: if on kamika route (helped her but sabotaged at the end)

label s15kamika:
show kamika u pout:
    xzoom -1.0
voice "voice/c-15-103.ogg" #Kamika (Ariane Marchese)
kam "Still, it bums me out. I really hoped that we could've gone all the way..."

voice "voice/c-15-104.ogg" #Yumi (Kathy Pfautsch)
yum "What!? What're you talking about??"

show kamika u disappointed:
    xzoom 1.0
voice "voice/c-15-105.ogg" #Kamika (Ariane Marchese)
kam "Taking over the school, and then further beyond. But I knew you were planning on double-crossing me. Everyone does."

show lucca h ohno:
    xzoom -1.0
voice "voice/c-15-106.ogg" #Yumi (Kathy Pfautsch)
yum "That's not what this is about! I didn't want you to go and do something incredibly stupid!"

show kamika u glare:
    xzoom 1.0
voice "voice/c-15-107.ogg" #Kamika (Ariane Marchese)
kam "Oh {i}please{/i}, you've been waiting to watch me fail like everyone else does! Snarking about me behind my back..."

show kamika u confident:
    xzoom -1.0
voice "voice/c-15-108.ogg" #Kamika (Ariane Marchese)
kam "But not this time! This time I'm surrounded by adoring fans! Devoted followers!"

show kamika u shout:
    xzoom 1.0
voice "voice/c-15-109.ogg" #Kamika (Ariane Marchese)
kam "They're more faithful than you ever were!"

voice "voice/c-15-110.ogg" #Yumi (Kathy Pfautsch)
yum "Kamika..."

"She spat venom at me, but I could tell she was hurt. Still, this wasn't the time or place to talk like adults over this..."
jump s15merge

#merge



label s15merge:

scene dormroomparty with dissolve

"With her piece said, Kamika sauntered off towards the middle of the room with a taunting smile."

show kamika u evilsmilesquint with easeinright:
    align (1.0, 1.0)
    xzoom -1.0
voice "voice/c-15-111.ogg" #Kamika (Ariane Marchese)
kam "Hey everyone! You guys having a good time!?"

#sfx:
play sound cheer
"The crowd responded with a rousing cheer. This party wasn't losing steam anytime soon."

scene dormroomparty
"I ran a hand through my hair, reviewing my options. God, I might have to bunk with Stacey or something..."

show moe neutrallook with easeinleft:
    align (0.35, 1.0)
    xzoom 1.0
voice "voice/c-15-112.ogg" #Moe (CJ Heineman)
moe "Mm. Looks like you're in a bit of trouble."

voice "voice/c-15-113.ogg" #Yumi (Kathy Pfautsch)
yum "Rrgh..."

show moe neutralclosed:
    xzoom -1.0
voice "voice/c-15-114.ogg" #Moe (CJ Heineman)
moe "Well, I'm properly hydrated, so I guess I could help out."

show lucca h nervous with dissolve:
    align (0.7, 1.0)
    xzoom 1.0
voice "voice/c-15-115.ogg" #Lucca (Victoria Wong)
luc "...Eh? What're you suggesting?"

show kamika u evilsmile with easeinright:
    align (1.1, 1.0)
    xzoom -1.0
voice "voice/c-15-116.ogg" #Kamika (Ariane Marchese)
kam "Ha! You're too late, Moe! They're already under my thumb, you see!?"

show moe smugclosed:
    xzoom 1.0
voice "voice/c-15-117.ogg" #Moe (CJ Heineman)
moe "...Y'sure about that?"

show kamika u mad:
    xzoom -1.0
show lucca h nervouser:
    xzoom 1.0
voice "voice/c-15-118.ogg" #Lucca (Victoria Wong)
luc "...O-oh gosh, you're not gonna... y-you can't, they're already-"

show moe sighsweat:
    xzoom -1.0
voice "voice/c-15-119.ogg" #Moe (CJ Heineman)
moe "Wow, you're real slow on the uptake, aren'tcha? Alright, I'll spell it out slowly."

show moe neutral:
    xzoom 1.0
voice "voice/c-15-120.ogg" #Moe (CJ Heineman)
moe "The punch is pretty much useless, guys. Her pheromones lost their potency in the punch."

show kamika u wideeyes:
    xzoom -1.0
show lucca h aaaa:
    xzoom 1.0
voice "voice/c-15-121.ogg" #Kamika (Ariane Marchese)
kam "...Eh? What?"

show moe squint:
    xzoom -1.0
voice "voice/c-15-122.ogg" #Moe (CJ Heineman)
moe "The most it did is add a bit of heat to the flavour profile..."

show kamika angrysurprise:
    xzoom 1.0
voice "voice/c-15-123.ogg" #Kamika (Ariane Marchese)
kam "Y-you're lying! I can tell! It's a bluff!"

show stacey therocklook with easeinleft:
    align (0.05, 1.0)
voice "voice/c-15-124.ogg" #Stacey (Ashe Thurman)
sta "Me and Moe basically downed the rest of it. I'm getting a bit of a buzz, but I think it's the rum..."

show kamika u stew:
    xzoom -1.0
show lucca h neutral:
    xzoom 1.0
show moe neutral:
    xzoom -1.0
voice "voice/c-15-125.ogg" #Moe (CJ Heineman)
moe "Trust me. It's more effective airborne. If you set up a fog machine or something, things might be different."

show moe smugclosed:
    xzoom 1.0
voice "voice/c-15-126.ogg" #Moe (CJ Heineman)
moe "...Though your pheromones were always on the weaker side. You were always a preeeetty shitty demon of lust."

show kamika u glare:
    xzoom -1.0
voice "voice/c-15-127.ogg" #Kamika (Ariane Marchese)
kam "Nrgh! You dare insult me in my realm!? I'd like to see you do better."

voice "voice/c-15-128.ogg" #Yumi (Kathy Pfautsch)
yum "This isn't your realm, Kamika..."

show moe neutralclosed:
    xzoom 1.0
voice "voice/c-15-129.ogg" #Moe (CJ Heineman)
moe "Eh. I could probably do better. Yumi's totally freaking out so..."

show stacey smugclosed
voice "voice/c-15-130.ogg" #Stacey (Ashe Thurman)
sta "Oh boyyy. Are you turning on the smoulder?"

show moe sigh:
    xzoom -1.0
show lucca h neutral:
    xzoom 1.0
voice "voice/c-15-131.ogg" #Moe (CJ Heineman)
moe "...I suppose I shall. If I must."

stop music fadeout 1.0
scene dormroomparty with dissolve
"He took a deep breath, and stepped back, getting some quick shoulder stretches in."

"He flicked his wrist and pointed skywards..."

voice "voice/c-15-132.ogg" #Moe (CJ Heineman)
moe "...Let's turn up the heat."

#rgb the room with a slight pink hue
show cg sexfogback zorder 1
show cg sexfogfront zorder 2

"All of a sudden, the room was filled with a pink haze! Where did it all come from?"

"Part of me didn't want to know, actually... it would probably ruin the mystique of it all."

"The party was reduced to a murmur, as all eyes were on him. They were rendered speechless, the din of the music falling deaf on their ears."


show moesmoulder default with dissolve
play music moetheme fadein 1.0

voice "voice/c-15-133.ogg" #Moe (CJ Heineman)
moe "Hey. Boys and girls. And any of you delicious non-binaries too. You looking for a good time?"

voice "voice/c-15-134.ogg" #Moe (CJ Heineman)
moe "If you're feeling a little hot under the collar... you may relieve yourself of your clothing over at my place.~"

voice "voice/c-15-135.ogg" #Kamika (Ariane Marchese)
kam "Wh-what're you doing!? Stop that this instant!"

"The crowd was mystified. I could see why. Moe presented himself in an all-new light now."

"It was... so different. Kamika's cries for attention fell on deaf ears."


show moesmoulder maximum with dissolve
voice "voice/c-15-136.ogg" #Moe (CJ Heineman)
moe "All you have to do is follow me. I'll take you to places you've only dreamed about.~"

"He shone so brilliantly! It was hard to look away...!"

"That was enough to draw the crowd closer to him. They staggered forward like zombies."

voice "voice/c-15-137.ogg" #Stacey (Ashe Thurman)
sta "Ah- Yumi, watch your step."

voice "voice/c-15-138.ogg" #Yumi (Kathy Pfautsch)
yum "W-what? What was I... o-oh shit."

voice "voice/c-15-139.ogg" #Stacey (Ashe Thurman)
sta "It's cool. Shit happens."

stop music fadeout 4.0
scene dormroomparty with dissolve
"I could only watch in amazement as Moe led the crowd out of the room, like the Pied Piper playing his tune. None of us said a word."
"...Well, Kamika said words. Most of them were expletives."

show kamika u panic with easeinright:
    align (0.1, 1.0)
    xzoom -1.0
voice "voice/c-15-140.ogg" #Kamika (Ariane Marchese)
kam "Wait, guys! Come back! I-I can make cupcakes!!"

#sfx
play sound doorclose

show kamika u sad:
    xzoom 1.0
"None of them reciprocated their attention as Kamika was faced with the door closing on her."

show lucca h bashful with dissolve:
    align (0.8, 1.0)
    xzoom -1.0
voice "voice/c-15-141.ogg" #Lucca (Victoria Wong)
luc "...I-it really is like night and day with Moe, huh..."

show stacey neutralclosed with dissolve:
    align (0.55, 1.0)
    xzoom 1.0
voice "voice/c-15-142.ogg" #Stacey (Ashe Thurman)
sta "Yeah. I like that side of him too.~"

show kamika u sadclosed:
    xzoom -1.0
voice "voice/c-15-143.ogg" #Kamika (Ariane Marchese)
kam "But... but I..."

show kamika u thinkingclosed:
    xzoom 1.0
show stacey neutral:
    xzoom -1.0
voice "voice/c-15-144.ogg" #Kamika (Ariane Marchese)
kam "...No, no, it's not over yet, I just need to... ah..."

show kamika u wideeyes:
    xzoom 1.0
voice "voice/c-15-145.ogg" #Kamika (Ariane Marchese)
kam "Fog machine! Yes, from the drama department! And then I can get it airborne and-"

show kamika u desperate:
    xzoom -1.0
voice "voice/c-15-146.ogg" #Kamika (Ariane Marchese)
kam "A-and then, and then! I-I'll just pump it over the school! O-or through the ventilation! Way better than some dumb party!"

show kamika u desperateclosed:
    xzoom 1.0
voice "voice/c-15-147.ogg" #Kamika (Ariane Marchese)
kam "Ehehe, yes, yes! This is perfect, flawless, it's going to definitely work-"

#sfx:
play sound throw
show kamika u wideeyes
show stacey eyebrow:
    xzoom -1.0
show lucca h ohno:
    xzoom 1.0
"{b}WHOOSH!{/b}"
#sfx pillow hitting sound effect

show kamika u ouch
voice "voice/c-15-148.ogg" #Kamika (Ariane Marchese)
kam "Oof!"

#sfx
play sound thud
hide kamika with vpunch
"And just like that, she crumpled to the floor..."

voice "voice/c-15-149.ogg" #Yumi (Kathy Pfautsch)
yum "...Party's over, Kamika."

voice "voice/c-15-150.ogg" #Lucca (Victoria Wong)
luc "H-holy cow, how fast did she throw that pillow??"

show stacey therocklook with easeinright:
    align (0.4, 1.0)
    xzoom -1.0
voice "voice/c-15-151.ogg" #Stacey (Ashe Thurman)
sta "Damn. I'm glad she's on our side... I think Kamika's out cold."

voice "voice/c-15-152.ogg" #Kamika (Ariane Marchese)
kam "...Waaah..."

show stacey neutrallook:
    xzoom 1.0
voice "voice/c-15-153.ogg" #Stacey (Ashe Thurman)
sta "Oop, nevermind, she's fine. Phew, I felt bad for a second there."

voice "voice/c-15-154.ogg" #Yumi (Kathy Pfautsch)
yum "Good. Great. Fantastic. {i}God{/i} I need a breather..."

show lucca h nervous with easeinright:
    align (0.65, 1.0)
voice "voice/c-15-155.ogg" #Lucca (Victoria Wong)
luc "W-where's Moe gonna take the party?"

show stacey neutral
voice "voice/c-15-156.ogg" #Stacey (Ashe Thurman)
sta "He said he'd take them over to his place, but... I dunno for sure. Who knows?"

show stacey smug
voice "voice/c-15-157.ogg" #Stacey (Ashe Thurman)
sta "Hopefully they don't all catch bedbugs over at his place or something. Heh."

show lucca h nervouser:
    xzoom -1.0
voice "voice/c-15-158.ogg" #Lucca (Victoria Wong)
luc "...Bedbugs?"

voice "voice/c-15-159.ogg" #Yumi (Kathy Pfautsch)
yum "...Pfft. Yeah. {i}Bedbugs.{/i} Heheh..."

show lucca h nervousclose:
    xzoom 1.0
voice "voice/c-15-160.ogg" #Lucca (Victoria Wong)
luc "W-what's so funny? Bedbugs are a serious matter..."

show stacey grinclosed
voice "voice/c-15-161.ogg" #Stacey (Ashe Thurman)
sta "Haha, yeah. Don't wanna wake up with {i}bedbugs{/i} all over you.~"

voice "voice/c-15-162.ogg" #Yumi (Kathy Pfautsch)
yum "Oh my gosh stop, ahahahaha!"

show lucca h bashful
voice "voice/c-15-163.ogg" #Lucca (Victoria Wong)
luc "I-I don't get you guys at all..."


"Stacey and I had a good laugh, to Lucca's dismay. I needed a good laugh myself. I think we all did."

scene black with dissolve

"We decide it's a good idea to convene right outside the door. Kamika could use some time alone, think about things a bit."

"We'd need a plan for how to go from here, and to keep Kamika from getting into any more shenanigans..."

jump scene16
