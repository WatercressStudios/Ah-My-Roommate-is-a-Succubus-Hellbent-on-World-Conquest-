label scene9i:
scene classroom with dissolve

"Something about class this morning... really doesn't feel right. I have this low, humming feeling of dread deep in my gut, as though I'm slowly realizing I overdrafted my bank account."

"When I was gathering my things, a classmate I didn't recognize came up to me to tell me they thought the question I asked during the lecture was really smart."

"What scares me is I don't remember {i}asking{/i} any questions."

"Ugh, it's all these demons... they're getting in my head, I guess. Which, come to think of it, is pretty much what people expect demons to do."

scene campus with dissolve
"After class, I head back towards the dorm rooms, but I don't get far before I'm distracted by a particular ten-gallon hat hurrying towards my position."

"That can't be good... God, I knew it. I {i}knew{/i} there was going to be another shoe dropping today."

"I guess the sooner I get it over with, the better..."

show lucca aaaa with easeinleft
voice "c-9i-1.wav" #Lucca (Victoria Wong)
luc "Yumi!"

"She sounds a little out of breath, as though she came running from the opposite side of campus."

voice "c-9i-2.wav" #Yumi (Kathy Pfautsch)
yum "Hey. What's going on?"

show lucca bashful
voice "c-9i-3.wav" #Lucca (Victoria Wong)
luc "I j-just wanted to ask... do you know if Kamika is up to anything today?"

voice "c-9i-4.wav" #Yumi (Kathy Pfautsch)
yum "She did say something to that effect this morning, yes. That she was \"putting things into motion\"."

show lucca nervous
"Though it almost got lost amidst my anger at her {i}literally crawling into my bed{/i} last night..."

voice "c-9i-5.wav" #Lucca (Victoria Wong)
luc "Well, I just s-saw her running around campus... She had a b-bunch of electronics with her."

voice "c-9i-6.wav" #Yumi (Kathy Pfautsch)
yum "Electronics?"

show lucca nervouser
voice "c-9i-7.wav" #Lucca (Victoria Wong)
luc "I... I don't know. but I have a b-bad feeling about it. The t-trouble she might get up to. Shouldn't we go and check on her?"

"A part of me wonders why it has to be \"we\" who does that, but knowing Lucca, she probably feels too nervous about confronting Kamika by herself, to the point that even having a Muggle like myself around gives her more courage."

"Wait, a \"Muggle\"? Ugh. too much of this crap is starting to rub off on me."

"A part of me is really starting to miss who I was, before my brother welcomed all of this into my life'but I don't miss who {i}he{/i} was before that happened, so maybe this is just the price I pay?"

"A Faustian bargain... "
extend "Oh, hell, I'm doing it again!"

voice "c-9i-8.wav" #Yumi (Kathy Pfautsch)
yum "Sure, I guess we'd better have a look at what she's doing."

show lucca neutral
voice "c-9i-9.wav" #Yumi (Kathy Pfautsch)
yum "Also... are you noticing that there's an unusual amount of students heading toward the plaza?"

voice "c-9i-10.wav" #Yumi (Kathy Pfautsch)
yum "I haven't heard of any events taking place there today'"

voice "c-9i-11.wav" #Lucca (Victoria Wong)
luc "Oh, dear. We'd better g-go see."

scene plaza with dissolve
"The two of us start hustling towards the plaza, and I thank my lucky stars that I'm not carrying a massive sack of art supplies with me this morning."

"As we're rushing along, a thought pops into my head, and I puzzle over it silently before giving the words form."

voice "c-9i-12.wav" #Yumi (Kathy Pfautsch)
yum "Hey, Lucca?"

show lucca neutral with dissolve
voice "c-9i-13.wav" #Lucca (Victoria Wong)
luc "Yes?"

voice "c-9i-14.wav" #Yumi (Kathy Pfautsch)
yum "That Kamika... she can't {i}really{/i} do anything horrible, can she? I mean if somebody could have done something they would have done it by now, yeah?"

show lucca guilty
"A vaguely apologetic expression crosses her face, and I realize the reassurance I'm seeking isn't on its way."

voice "c-9i-15.wav" #Lucca (Victoria Wong)
luc "Well... There are t-technically certain avenues by which she c-could succeed..."

voice "c-9i-16.wav" #Yumi (Kathy Pfautsch)
yum "God. You're serious."

show lucca happy
voice "c-9i-17.wav" #Lucca (Victoria Wong)
luc "However! I don't think she'd ever get very far with c-certain, large-scale plans. There's... contingencies."

voice "c-9i-18.wav" #Yumi (Kathy Pfautsch)
yum "Contingencies, eh? But are they {i}good{/i} contingencies?"

show lucca neutral
voice "c-9i-19.wav" #Lucca (Victoria Wong)
luc "Er. No. Very much no."

voice "c-9i-20.wav" #Yumi (Kathy Pfautsch)
yum "Really? That bad?"

show lucca bashful
voice "c-9i-21.wav" #Lucca (Victoria Wong)
luc "Um... yes."

"She runs a hand sheepishly through her hair, glancing skyward, and sighs."

show lucca nervousclose
voice "c-9i-22.wav" #Lucca (Victoria Wong)
luc "For example, there was this cambion, a-about a hundred and t-ten years ago... Barry."

##VA delivers this line incredulously
voice "c-9i-23.wav" #Yumi (Kathy Pfautsch)
yum "...Barry."

show lucca happy
voice "c-9i-24.wav" #Lucca (Victoria Wong)
luc "Short for Barbas. He n-never really got along with anyone. Cambions can be so touchy..."

show lucca nervoussmile
voice "c-9i-25.wav" #Lucca (Victoria Wong)
luc "Anyway, he, um, g-gathered followers out somewhere in Siberia, and bragged about t-turning it into his personal fiefdom."

voice "c-9i-26.wav" #Yumi (Kathy Pfautsch)
yum "So what happened?"

show lucca neutral
voice "c-9i-27.wav" #Lucca (Victoria Wong)
luc "I'm not really sure, but... m-millions of poor trees were f-flattened."

##Yumi horrified

voice "c-9i-28.wav" #Yumi (Kathy Pfautsch)
yum "{i}What?!{/i}"

show lucca  nervoussmile
voice "c-9i-29.wav" #Lucca (Victoria Wong)
luc "Oh, but I don't think that'll happen with Kamika! You'll manage to talk some sense into her, I just know it!"

voice "c-9i-30.wav" #Yumi (Kathy Pfautsch)
yum "Oh, geez, but no pressure, huh?"

show lucca bashful
voice "c-9i-31.wav" #Lucca (Victoria Wong)
luc "P-please, try not to worry about it... That sort of thing doesn't h-happen very often anymore, because they're so hard to c-cover up."

"Literally everything that just came out of Lucca's mouth is absolutely the worst."

"As I quietly walk alongside her, I realize that I will never be able to learn about some unusual tragedy again without quietly wondering if the official story's just a smokescreen for a messily smote demon."

"I wonder if this is what they say about knowledge of the supernatural being corrupting and dangerous..."

"...Great. I'm doing it {i}again{/i}."

"When this is over and done with, I swear I'm going to see what my options are for a lobotomy."

show lucca happyclosed
voice "c-9i-32.wav" #Lucca (Victoria Wong)
luc "Anyway, if you're concerned, maybe w-we should team up? A-and figure out a plan to h-handle her?"

voice "c-9i-33.wav" #Yumi (Kathy Pfautsch)
yum "Hmm..."

menu:
    "Agree to collaborate":
        $ lucca_points += 1
        jump s9ilucca
    "I can handle it on my own":
        jump s9iedgelord
#CHOICE:
#>>Agree to collaborate
#>>I can handle it on my own

label s9ilucca:

#BRANCH: dialogue path from ">>Agree to collaborate"

"Teaming up doesn't sound like a bad idea. And Lucca is the most knowledgeable person I know about this."

voice "c-9i-34.wav" #Yumi (Kathy Pfautsch)
yum "Yeah, maybe later on we can put our heads together and work on a strategy to, um, defuse this volatile situation."

voice "c-9i-35.wav" #Yumi (Kathy Pfautsch)
yum "I'd really appreciate your input, since you've probably known her a lot longer than me."

show lucca bashful
voice "c-9i-36.wav" #Lucca (Victoria Wong)
luc "Well, longer doesn't always mean b-better, heh. But I'll absolutely help however I can!"

jump s9imerge

label s9iedgelord:
#BRANCH: dialogue path from ">>I can handle it on my own"


"As much as I appreciate Lucca wanting to help, I don't want to get her wrapped up in Kamika's nonsense if I can help it."
"Besides, I think I know another angle I want to tackle this from."

voice "c-9i-37.wav" #Yumi (Kathy Pfautsch)
yum "Well, you know, I wouldn't want to put you out... and who knows, maybe Kamika's diabolical plan is totally impotent."

show lucca neutral
voice "c-9i-38.wav" #Yumi (Kathy Pfautsch)
yum "If all that this whole thing boils down to is having a stinky, self-important, obnoxious roommate with delusions of grandeur, well, that's something I already know how to handle. I'd feel stupid if we held strategy sessions just for that."

show lucca sigh
voice "c-9i-39.wav" #Lucca (Victoria Wong)
luc "That's... true, I suppose."

jump s9imerge

#MERGE:

label s9imerge:

voice "c-9i-40.wav" #Lucca (Victoria Wong)
luc "Oh! Look!"

hide lucca with dissolve
"In the distance, I can definitely see the crowd becoming denser around the plaza. Something's seriously going on."

voice "c-9i-41.wav" #Yumi (Kathy Pfautsch)
yum "What do you think she's up to?"

show lucca ohno flip
voice "c-9i-42.wav" #Lucca (Victoria Wong)
luc "I... I'm not entirely certain. Kamika can be so hard to predict..."

voice "c-9i-43.wav" #Yumi (Kathy Pfautsch)
yum "Well, you're both demons, right? How would {i}you{/i} try to take over the world, if you were so inclined?"

show lucca bigaaaa flip

"The look she gives in response is one of the utmost horror. It's as though I just told her there was an entire frog in her smoothie."

voice "c-9i-44.wav" #Lucca (Victoria Wong)
luc "I... I would n-never even dream...!"

voice "c-9i-45.wav" #Yumi (Kathy Pfautsch)
yum "I know. But you have to know certain tricks of the trade, right?"

show lucca mad flip
"She just shakes her head ardently."

voice "c-9i-46.wav" #Lucca (Victoria Wong)
luc "That just isn't what our talents are for! M-most of us know this instinctively. Kamika r-rejects it outright."

voice "c-9i-47.wav" #Yumi (Kathy Pfautsch)
yum "*sigh*"

show lucca neutral flip
voice "c-9i-48.wav" #Yumi (Kathy Pfautsch)
yum "Well, that's very admirable, but it leaves me pretty flat-footed in terms of having any idea what she's up to."

show lucca happy flip
"But Lucca just offers me a warm, reassuring smile."

voice "c-9i-49.wav" #Lucca (Victoria Wong)
luc "Not to worry! Another thing Kamika rejects outright is subtlety."

"Right, right... After that debacle with Satan's phone number, I really shouldn't have forgotten how true that is of {i}most{/i} of her kind."

"I smile back at Lucca, but the moment barely lasts before I finally catch a glimpse of what it is the students are gathering around."

voice "c-9i-50.wav" #Yumi (Kathy Pfautsch)
yum "That's a soundstage..."

show lucca nervous flip
voice "c-9i-51.wav" #Lucca (Victoria Wong)
luc "Oh, dear."

"Sure enough, there's all sorts of audio equipment around, and between the throng of people I can see Kamika futzing over it."

"How she got all this expensive stuff, or how she even got herself {i}authorized{/i} to set it all up in the middle of a public space like this. is quite beyond my comprehension, and after the day I've had I think it'd be best to keep it that way."

show lucca nervouser flip
voice "c-9i-52.wav" #Lucca (Victoria Wong)
luc "She's g-going to sing..."

voice "c-9i-53.wav" #Yumi (Kathy Pfautsch)
yum "Crap. Is that going to hypnotize everyone?"

show lucca neutral flip
"For a moment, Lucca just gives me a stare so blank I could print a resume on it."

voice "c-9i-54.wav" #Lucca (Victoria Wong)
luc "...Succubi and sirens are two {i}totally different{/i} things, Yumi."

voice "c-9i-55.wav" #Yumi (Kathy Pfautsch)
yum "Right, I knew that. Of course."

hide lucca with dissolve
"I mean, if nothing else, her singing in the shower didn't have any effect on me, so I probably could have guessed that."

show kamika u confident flip with easeinright
"As we're discussing amongst ourselves what her dastardly agenda might be, Kamika steps to the front of the platform with a microphone, looking for all the world as smug as a politician that just stole an election."

show kamika u happy flip
voice "c-9i-56.wav" #Kamika (Ariane Marchese)
kam "Hello there, my dears~! It is I, the one and only Kamika Malagic~!"

show kamika u happyclosed flip
voice "c-9i-57.wav" #Kamika (Ariane Marchese)
kam "I'm here to give the students of the Salem School for Calligraphy, Art, and Music the best time of their lives, so please, turn your attention to me as I sing you this song!"

"With a more forced smile, she turns around and awkwardly trots back to where her music player is set up and pushes a button before briskly returning to the head of the stage."

"She brings the microphone to her lips, and then..."

##SFX: The worst sound fucking imaginable
show kamika u wideeyes flip with hpunch

voice "c-9i-58.wav" #Yumi (Kathy Pfautsch)
yum "Ack!!"

hide kamika with dissolve
"Whatever she was about to sing gets utterly drowned out by the most godawful cacophony of noise I've ever heard in my life."

"It must be an audio feedback loop combined with... I don't know. I can't even tell what song is playing, with how far it's pitched up - and there's zero bass whatsoever, like the bizarro universe version of that horrible pop song."

"It's incredible - with this sonic atrocity assaulting my senses, somehow Kamika has captured the exact timbre of what I imagine Hell sounds like {i}all the time{/i}."

voice "c-9i-59.wav" #Female Student)
fst "Aaaaaaaugh! Turn it off! Please turn it off!"

voice "c-9i-60.wav" #Lucca (Victoria Wong)
luc "This is terrible..."

"Whatever's going on, it certainly doesn't seem to be Kamika's intention, at least if the absolutely horrified expression on her face is any indication. Her face is as beet-red as I've ever seen it."

"She dashes back to the sound system - an impressive feat, with the heels she's in - and tries to correct the blasphemous shrieking coming out of the speakers, but all she succeeds in doing is modulating the sound into a nauseating low hum, and then a sharp, angry static."

show lucca ohno with dissolve
voice "c-9i-61.wav" #Yumi (Kathy Pfautsch)
yum "Oh, she didn't set up her equipment properly!"

voice "c-9i-62.wav" #Lucca (Victoria Wong)
luc "...What?"

voice "c-9i-63.wav" #Yumi (Kathy Pfautsch)
yum "I said, SHE DIDN'T SET UP HER EQUIPMENT PROPERLY!"

show lucca uwah
voice "c-9i-64.wav" #Lucca (Victoria Wong)
luc "...WHAT?"

voice "c-9i-65.wav" #Yumi (Kathy Pfautsch)
yum "NEVER MIND!"

voice "c-9i-66.wav" #Lucca (Victoria Wong)
luc "SORRY, I CAN'T HEAR YOU BECAUSE SHE DIDN'T SET UP HER EQUIPMENT PROPERLY."

hide lucca with dissolve
"As Kamika's ineffectual and increasingly desperate fiddling continues, much of the crowd of students is fleeing into the distance, or into the mild insulation of the buildings around them."

"Much of the body of students remaining only seems to be enduring the noise to laugh at her - or cringe at her."

show kamika u surprise flip with dissolve:
    align (0.7, 1.0)
voice "c-9i-67.wav" #Kamika (Ariane Marchese)
kam "No. No! This can't be happening!"

"She messes with the equipment even more, but it's no good at all. Watching her plan slip through her fingers like this is... actually seriously depressing."

"Even though she's the most obnoxious person in the world, I loathe every second of this moment."

"Finally, mercifully, she kills the power to the system, and the ensuing silence falling upon the plaza is almost hauntingly viscous."

"For a second, she remains huddled over the deactivated stereo equipment like a child whose dog just got run over."

"It's a sad tableau that remains undisturbed until some guy in the crowd sheepishly approaches her."

voice "c-9i-68.wav" #Male Student (???)
mst "Hey, hey, it's okay. The audio system isn't set up right, that's all. Here, just gimme a sec to fix it and you'll be right as rain..."

show kamika u mad
"But as soon as he tries to gently place a hand on her shoulder, she spins around and wallops the Good Samaritan right in the face with a surprisingly stiff hand. Beside me, Lucca lets out a gasp."

voice "c-9i-69.wav" #Kamika (Ariane Marchese)
kam "How dare you! You'll live to regret your loathsome pity, you insignificant whelp!"

"...Welp! So much for that."

"Well, she's right about one thing: I immediately regret my {i}own{/i} pity."

show kamika u pout flip
"Apparently unwilling to shoulder this kind of attention anymore - and with the remaining crowd closing in a little, after she smacked that one guy on his ass..."

show kamika u whine
hide kamika with easeoutright
"...She runs off, totally abandoning the sound equipment behind her."

"Unfortunately, she's running approximately in {i}my{/i} direction, and as she sees me, she changes course, sprinting towards me with an expression of barely contained rage."

"I almost take off myself - I can probably outrun her, in my somewhat more sensible shoes - but I realize there's no point, and I stand my ground for whatever nonsense this is going to be."

show kamika u angrysurprise with dissolve
voice "c-9i-70.wav" #Kamika (Ariane Marchese)
kam "{i}You!{/i} You {i}contemptible{/i} little reprobate!"

voice "c-9i-71.wav" #Yumi (Kathy Pfautsch)
yum "{i}Me?!{/i} What the hell did {i}I{/i} do?"

show kamika u mad
voice "c-9i-72.wav" #Kamika (Ariane Marchese)
kam "You {i}stood{/i} there and beheld your own {i}incompetence{/i} written large, like the insect you are, and set back my ascension immeasurably!"

show kamika u madclosed
voice "c-9i-73.wav" #Kamika (Ariane Marchese)
kam "All you needed to do was look out for our mutual best interests and proffer your assistance like a good little soldier, but no... you can't even be bothered to do something so simple as handle a bit of basic technology for your queen!"

show kamika u thinkingclosed
voice "c-9i-74.wav" #Kamika (Ariane Marchese)
kam "No, no, that was {i}far{/i} too much effort to entrust the likes of you with. You can only be safely relied upon to show up to witness the consequences of your selfish indolence!"

show kamika u glare
voice "c-9i-75.wav" #Kamika (Ariane Marchese)
kam "I assure you, when the history of my conquests has been written large across the collective memory of your lowly species, generation after generation will baffle over why their glorious Lady ever hoped to entrust her destiny to the hands of such a gormless, insubordinate wretch such as yourself!"

voice "c-9i-76.wav" #Yumi (Kathy Pfautsch)
yum "Listen, you stupid, utterly deluded...."

show lucca mad with easeinright:
    align (0.3, 1.0)
show kamika u wideeyes
voice "c-9i-77.wav" #Lucca (Victoria Wong)
luc "L-leave her alone, Kamika!"

"I'm cut off by an unexpected party rising to my defense."

"Wow. Lucca? Standing up to Kamika? Standing up to {i}anyone{/i}? Never thought I'd see the day."

show kamika u mad
voice "c-9i-78.wav" #Kamika (Ariane Marchese)
kam "{i}Excuse{/i} me? Who would dare..."

show kamika u smug flip with easeinright:
    align (0.66, 1.0)
extend " oh. Oho, now I believe I see what's going on, here."

show kamika u evilsmile flip
voice "c-9i-79.wav" #Kamika (Ariane Marchese)
kam "Why do you befoul these grounds with your ghastly presence, Lucy? Come back around for another twist of the knife?"

show lucca madclosed
voice "c-9i-80.wav" #Lucca (Victoria Wong)
luc "I don't even kn-know what you're talking about, but..."

show kamika u glare flip
voice "c-9i-81.wav" #Kamika (Ariane Marchese)
kam "I see now that it was misguided to blame this defeat upon my intended minion. How confused must she have been, with all the {i}susurrous seditions{/i} you've been hissing into her virgin ears!"

voice "c-9i-82.wav" #Yumi (Kathy Pfautsch)
yum "...Um."

show lucca madclosed
voice "c-9i-83.wav" #Lucca (Victoria Wong)
luc "Or m-maybe she's just a normal g-girl that doesn't want any part of your harebrained schemes. Did y-y-you consider that?"

show kamika u confident flip
voice "c-9i-84.wav" #Kamika (Ariane Marchese)
kam "Pfft. Save your breath, deceiver. I've heard enough of your poison to drown an eternity."

show kamika u confidentclosed flip
voice "c-9i-85.wav" #Kamika (Ariane Marchese)
kam "Or do you think I didn't learn of your last little insult?"

show kamika u nervous flip
voice "c-9i-86.wav" #Lucca (Victoria Wong)
luc "What are you even t-talking about?"

show kamika u mad flip
voice "c-9i-87.wav" #Kamika (Ariane Marchese)
kam "Oh, please, as if it were anything but wanton mockery when you'd brought that grungy skink into your collective."

voice "c-9i-88.wav" #Yumi (Kathy Pfautsch)
yum "...Don't you mean \"skank\"?"

show kamika u glare flip
voice "c-9i-89.wav" #Kamika (Ariane Marchese)
kam "Did I stutter, mortal?"

show kamika u sigh flip
voice "c-9i-90.wav" #Lucca (Victoria Wong)
luc "Levi? Levi's not a m-{i}mockery{/i} of you! People aren't mockeries of {i}other people{/i}, Kamika!"

show kamika u madclosed flip
voice "c-9i-91.wav" #Lucca (Victoria Wong)
luc "What do you care, really? You'll just h-h-hate anything that gives you an excuse to feel slighted."

show kamika u madclosed flip
voice "c-9i-92.wav" #Kamika (Ariane Marchese)
kam "Forget it. There's no talking to you, you horrible little {i}hag{/i}."

show kamika u evilsmile flip
show lucca mad
voice "c-9i-93.wav" #Kamika (Ariane Marchese)
kam "Just know that there's nothing you, your hired gun, or any of your deplorable sisters can do to stand in the way of my ultimate destiny."

show kamika u smugclosed flip
voice "c-9i-94.wav" #Kamika (Ariane Marchese)
kam "Not even {i}this{/i} attempt at sabotage will do anything to stand in the way of that. It's true that the students of this school remain ignorant to the full depths of my beauty, but now they have learned to fear my wrath. I've been successful enough to move to the next phase."

show kamika u licklips flip
show kamika ohno
voice "c-9i-95.wav" #Kamika (Ariane Marchese)
kam "Goodbye for now. Feel free to hold onto my minion until I have use of her."

show kamika u glare
hide kamika with easeoutright
"And with that, she storms off towards the dorms, Lucca still angrily staring her down until she disappears into the trees."

show lucca madclosed
voice "c-9i-96.wav" #Lucca (Victoria Wong)
luc "...Ugh... She's just the worst..."

voice "c-9i-97.wav" #Yumi (Kathy Pfautsch)
yum "Truer words have never been spoken."

show lucca neutral
voice "c-9i-98.wav" #Yumi (Kathy Pfautsch)
yum "Still, what you did, staring her down like that, was pretty cool."

## Lucca blushing surprised

show lucca bashful
voice "c-9i-99.wav" #Lucca (Victoria Wong)
luc "Oh... Oh my. I didn't r-realize - I just couldn't stand her at-t-tacking you like that..."

voice "c-9i-100.wav" #Yumi (Kathy Pfautsch)
yum "I really appreciate it."

#if cutoff is too abrupt add narration here
jump scene10
